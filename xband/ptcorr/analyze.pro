;+
; NAME: analyze
;
; PURPOSE: 
;       an umbrella procedure which takes raw data from SCANNER and
;returns a list of pointing correction constants
;
; EXPLANATION: 
;       This procedure calls ERRORS, which reads in data from the
;       specified directory and uses polynomial baseline removal and a
;       2-D gaussian fitting procedure to produce a list of pointing
;       offsets. Those offsets are then used by POINTFIT, which
;       performs least squares fitting for the correction constants
;       used by CORRECT (a part of POINT2) in the file point.config2 
;
; CALLING SEQUENCE: 
;       analyze [,dirname,/i,/s,/usecor,order=#,fname=string]
;
; INPUTS:
;        None are required. If non are specified, program will prompt
;        user for a directory name
;        
;       dirname - the directory in which the files from SCANNER are
;                 found.  The words SUN and MOON are used to identify
;                 the correct files, so any stray files with these
;                 names will cause trouble unless they are proceeded
;                 with a '.'  You should probably move all the files
;                 you are want into a fresh directory.  Note
;                 especially that the "...fit.sav" files generated by
;                 SCANNER's auto correct mode will cause problems, and
;                 they should be deleted or moved before running
;                 ANALYZE.
;
;                ***NEW** - dirname can now be an array of string file
;                           names, such as ['dir1','dir2'].  The files
;                           need not have the same parameters, however
;                           you cannot mix oldschool and new files
;                           unless you explicitly treat them all as
;                           oldschool files.
;
; OPTIONAL INPUTS:
;       fname - the filename into which you want the intermediate and
;               log files saved.  It will be suffixed with _er or _pf
;               and an extension.  Generally this is only useful if
;               you want to compare the results of analysis with
;               different settings on the same data set.  
;       order=# - the order of the polynomial used in baseline
;               fitting. See docs for POINTFIT for more info.         
;       scoop=# - when scoop is called with a number other than 1 or
;                 zero, the gaussian fitting will only look at this
;                 many points in the vicinity of the global minimum.
;                 If called with value 1, the default number of points
;                 will be used.  If zero or not specified, then all
;                 the points will be used.  This is useful in order to
;                 avoid bad fits to wondering baslines or non-gaussian
;                 patterns due either to the actual horn pattern or
;                 nonlinearities in the detector.
;
; OPTIONAL INPUT KEYWORDS:
;       /i - interactive mode.  The default.
;       /s - silent mode. (You will not be able to inspect the fits)   
;       /usecorr - use corrected values - defines delta as the center
;                  of the object minus the point at which you were
;                  looking for the object AFTER having made the
;                  corrections based on your point.config2 file.
;                  THIS WILL PROBABLY CAUSE BAD THINGS TO HAPPEN
;                  unless you are very careful. Use it with caution. 
;       /mixcor -  plots things against the corrected values, but
;                returns the fitted constants with respect to the
;                absolute values.  Useful if you want to visually
;                inspect the accuracy of your corrections. Will not
;                work with nonzero scoop (until someone gets around to
;                writing that bit.)
;       /nobase - doesn't do any baseline fitting.
;       /constant - fits only for an overall average dial offset
;                  without any skew or flop
;       /oldschool - uses the center point as the expected value instead
;                   of the saved expected value.  Necessary in order
;                   to read in files make prior to June 10,2001,
;                   because the old version of scanner did not save
;                   the expected values.
;
; OUTPUTS: none.  (a log file is saved with the calculated corrections)
;
; EXAMPLES: analyze,'~/xband/ptcorr/4/'
;
; RESTRICTIONS: 
;       - values of i and s may be changed by this program 
;       - mixcor and usecor may not work with some other options, such
;       as scoop.
;
; PROCEDURES CALLED: 
;       ERRORS, POINTFIT, (aafit,fitgen,base_gauss2)
;
; REVISION HISTORY: written by Erik Shirokoff, 5/2001.  Modified
; continually by ES, summer 2001.
;	13mar03 by ch: changed some internal formatting to make code easier to
;read. 
;-

pro analyze,dirname,i=i,s=s,order=order,usecor=usecor,fname=fname, $
	oldschool=oldschool,constant=constant,scoop=scoop,nobase=nobase, $
	mixcor=mixcor

;GET DIRECTORY NAME
if n_params() eq 0 then begin

    print,'Enter the name of the directory where the scanner files are stored, or press ENTER to use the current directory.'
    Print,'NOTE: This directory must not contain any other files with names containing SUN or MOON.  Use doc_library for more info.'
    dirname=' '
    read,dirname,prompt='directory: '
endif

;HANDLES MULTIPLE OR SINGLE DIR CASE
numdirs=n_elements(dirname) 
if numdirs eq 1 then dirname=[dirname]
dirn=dirname
for nr=0,numdirs-1 do begin
dirn[nr]=smartname('./',dirname[nr])
endfor

if keyword_set(s) eq 0 then vv=1 else vv=0

if keyword_set(fname) then sn=fname+'_er'


errors,dirn,out,silent=keyword_set(s),order=order,usecor=usecor, $
savename=sn,oldschool=oldschool,nobase=nobase,mixcor=mixcor,scoop=scoop

dirn=dirn[0]

filename=dirn+'errors.sav'

print,'do you want to see the fits? (y / n)'
r=get_kbrd(1)
if strcmp(r,'y') then plotit=1

if keyword_set(fname) then sn=fname+'_pf'


pointfit,filename,cor,verbose=vv,plotit=plotit,logname=sn, $
	savename=sn,constant=constant

if keyword_set(fname) then print,'output log has been written to '+dirn+sn
print,'output log has been written to '+dirn+'pointfit.log'


end





